
//the generated ANTLR sources plus the OpenEHR Test RM implementation do not need coverage
def excludedPaths = ["**/nedap/archie/adlparser/antlr**", "**/nedap/archie/openehrtestrm/**"]

allprojects {
  apply plugin: 'jacoco'
}

subprojects {
  jacoco {
    toolVersion = "0.8.2"  
  }

  test {
     testLogging {
        events "failed"
        exceptionFormat "full"
    }
  }

  jacocoTestReport {
      reports {
          xml.enabled = true
          html.enabled = true
      }
      additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
      sourceDirectories = files(sourceSets.main.allSource.srcDirs)
      classDirectories =  files(sourceSets.main.output)
      afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
           fileTree(dir: it, excludes: excludedPaths)
        })
      }
  }

  check.dependsOn jacocoTestReport
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    //The following is added because otherwise jacoco fails with packages without any test code
    //as we have for example 'archie-all' that only combines the other projects in a single pom.xml, plus some 
    //packages that are tested in the tools package, this is required
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
    //end of code needed for packages without tests

    afterEvaluate {
      classDirectories = files(classDirectories.files.collect {
         fileTree(dir: it, excludes:  excludedPaths)
      })
    }
}